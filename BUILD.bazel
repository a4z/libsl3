load("@rules_cc//cc:defs.bzl", "cc_library")

package(default_visibility = ["//visibility:public"])

# Generate config header using module reflection
genrule(
    name = "generate_config",
    srcs = ["src/config.in"],
    outs = ["include/sl3/config.hpp"],
    cmd = """
        # Parse module version (format: 1.2.47002) - single source of truth
        VERSION='%s'
        MAJOR=$$(echo $$VERSION | cut -d. -f1)
        MINOR=$$(echo $$VERSION | cut -d. -f2)
        PATCH=$$(echo $$VERSION | cut -d. -f3)

        # Extract SQLite version from patch (47002 -> minor=47, patch=2)
        SQLITE_MINOR=$$(( $$PATCH / 1000 ))
        SQLITE_PATCH=$$(( $$PATCH %% 1000 ))

        sed -e 's/\\$${sl3_MAJOR_VERSION}/'$$MAJOR'/g' \
            -e 's/\\$${sl3_MINOR_VERSION}/'$$MINOR'/g' \
            -e 's/\\$${sl3_PATCH_VERSION}/'$$PATCH'/g' \
            -e 's/\\$${sl3_VERSION}/'$$VERSION'/g' \
            -e 's/\\$${internal_SQLITE_MINOR_V}/'$$SQLITE_MINOR'/g' \
            -e 's/\\$${internal_SQLITE_PATCH_V}/'$$SQLITE_PATCH'/g' \
            $< > $@
    """ % module_version(),
)

cc_library(
    name = "sl3",
    srcs = [
        "src/sl3/columns.cpp",
        "src/sl3/command.cpp",
        "src/sl3/config.cpp",
        "src/sl3/database.cpp",
        "src/sl3/dataset.cpp",
        "src/sl3/dbvalue.cpp",
        "src/sl3/dbvalues.cpp",
        "src/sl3/error.cpp",
        "src/sl3/rowcallback.cpp",
        "src/sl3/types.cpp",
        "src/sl3/value.cpp",
        # Private headers
        "src/sl3/connection.hpp",
        "src/sl3/utils.hpp",
    ],
    hdrs = [
        "include/sl3.hpp",
        "include/sl3/columns.hpp",
        "include/sl3/command.hpp",
        "include/sl3/container.hpp",
        "include/sl3/database.hpp",
        "include/sl3/dataset.hpp",
        "include/sl3/dbvalue.hpp",
        "include/sl3/dbvalues.hpp",
        "include/sl3/error.hpp",
        "include/sl3/rowcallback.hpp",
        "include/sl3/types.hpp",
        "include/sl3/value.hpp",
        ":generate_config",
    ],
    includes = [
        "include",
    ],
    deps = [
        "@sqlite//:sqlite3",
    ],
)
